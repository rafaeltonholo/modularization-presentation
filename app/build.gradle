plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "com.google.devtools.ksp" version "1.6.21-1.0.5"
}

android {
    compileSdk versions.compileSdk

    defaultConfig {
        applicationId "dev.tonholo.awesomeapp"
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.compose
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }

    buildTypes.each { type ->
        api_credentials.each { key, val ->
            type.buildConfigField "String", key, "\"$val\""
        }
    }
}

dependencies {

    implementation dep.androidx.core.ktx

    implementation dep.androidx.compose.ui
    implementation dep.androidx.compose.material3
    implementation dep.androidx.compose.uiToolingPreview
    implementation dep.androidx.compose.navigation

    implementation dep.androidx.lifecycle.runtime
    implementation dep.androidx.activity.compose

    implementation dep.hilt.android
    implementation dep.hilt.navigation_compose
    kapt dep.hilt.compiler

    implementation dep.androidx.datastore.preferences

    implementation dep.coil.compose

    implementation dep.androidx.room.runtime
    implementation dep.androidx.room.ktx
    implementation dep.androidx.room.paging
    ksp dep.androidx.room.compiler

    implementation dep.gson
    implementation dep.retrofit.main
    implementation dep.retrofit.converter_gson

    implementation dep.androidx.camera2.main
    implementation dep.androidx.camera2.view
    implementation dep.androidx.camera2.lifecycle

    testImplementation dep.junit
    androidTestImplementation dep.androidx.test.junit
    androidTestImplementation dep.androidx.test.espresso
    androidTestImplementation dep.androidx.test.composeUiTest
    debugImplementation dep.androidx.compose.debug.uiTooling
    debugImplementation dep.androidx.compose.debug.uiTestManifest
}
